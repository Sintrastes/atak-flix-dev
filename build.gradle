
import java.io.File

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

configurations {
    compileFlix
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def flixCompiler = properties.getProperty('flix.compiler')

dependencies {
    compileFlix files(flixCompiler)
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task compileFlix(type: JavaExec) {
    workingDir = "./"
    classpath = configurations.compileFlix
    args('build')
}

task packageFlix(type: JavaExec) {
    dependsOn "compileFlix"
    workingDir = "./"
    classpath = configurations.compileFlix
    args('build-jar')
}

task assembleFlix() {
    dependsOn "packageFlix"
    doLast {
        file("app/build/libs").mkdirs()
        file("atak-flix-dev.jar").renameTo("app/build/libs/atak-flix-dev.jar")
    }
}
